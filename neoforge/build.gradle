plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
            setGameDirectory(file('server'))
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
    repositories {
        // CurseMaven
        exclusiveContent {
            forRepository {
                maven {
                    url "https://cursemaven.com"
                }
            }
            filter {
                includeGroup "curse.maven"
            }
        }
        maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
        maven { url = "https://maven.ithundxr.dev/snapshots" } // Registrate
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    }

    dependencies {
        implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
        implementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}")
        compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
        runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
        implementation("com.tterrag.registrate:Registrate:${registrate_version}")

        implementation "curse.maven:farmers-delight-398521:5566383"

    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }